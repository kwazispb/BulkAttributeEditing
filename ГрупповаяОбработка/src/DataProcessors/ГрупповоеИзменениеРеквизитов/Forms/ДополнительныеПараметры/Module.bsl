&НаСервере
Процедура ГР_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)

	МассивИзменяемыхРеквизитов = новый Массив();
	МассивИзменяемыхРеквизитов.Добавить(новый РеквизитФормы("ГР_ВыполнитьКодПередЗаписьюЭлемента", новый ОписаниеТипов("Булево")));
	МассивИзменяемыхРеквизитов.Добавить(новый РеквизитФормы("ГР_КодПередЗаписью", новый ОписаниеТипов("Строка", , новый КвалификаторыСтроки())));
	МассивИзменяемыхРеквизитов.Добавить(новый РеквизитФормы("ГР_ВидыИзменяемыхОбъектов", новый ОписаниеТипов("Строка", , новый КвалификаторыСтроки())));

	ИзменитьРеквизиты(МассивИзменяемыхРеквизитов);
	ЭтаФорма.ГР_ВыполнитьКодПередЗаписьюЭлемента = Параметры.ГР_ВыполнитьКодПередЗаписьюЭлемента;
	ЭтаФорма.ГР_КодПередЗаписью = Параметры.ГР_КодПередЗаписью;
	ЭтаФорма.ГР_ВидыИзменяемыхОбъектов = Параметры.ГР_ВидыИзменяемыхОбъектов;

	ПолеФлажка = Элементы.Добавить("ГР_ВыполнитьКодПередЗаписьюЭлемента", Тип("ПолеФормы"));
	ПолеФлажка.Вид = ВидПоляФормы.ПолеФлажка;
	ПолеФлажка.ПутьКДанным = "ГР_ВыполнитьКодПередЗаписьюЭлемента";
	ПолеФлажка.Заголовок = "Выполнить код перед записью";
	ПолеФлажка.УстановитьДействие("ПриИзменении" ,"ГР_ПриИзменении_ГР_ВыполнитьКодПередЗаписьюЭлемента");
	
	НоваяГруппа = Элементы.Добавить("ГР_КоманднаяПанель",Тип("ГруппаФормы"));
	НоваяГруппа.Вид = ВидГруппыФормы.КоманднаяПанель;
	
	ПолеВвода = Элементы.Добавить("ГР_КодПередЗаписью", Тип("ПолеФормы"));
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "ГР_КодПередЗаписью";
	ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеВвода.МногоСтрочныйРежим = Истина;
	ПолеВвода.ВысотаЗаголовка = 0;
	ПолеВвода.Ширина = 80;

	Элементы.ГР_КодПередЗаписью.Видимость = ЭтаФорма.ГР_ВыполнитьКодПередЗаписьюЭлемента;
	Элементы.ГР_КоманднаяПанель.Видимость = ЭтаФорма.ГР_ВыполнитьКодПередЗаписьюЭлемента;
	
	Попытка
		Коды = ГР_ПолучитьТекстыОбработчиков();
	Исключение
		Коды = Неопределено;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru='Не удалось инициализировать шаблоны модулей для видов метаданных'");
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	Для Каждого КлючИЗначение из Коды Цикл
		Если ЭтаФорма.ГР_ВидыИзменяемыхОбъектов = СтрЗаменить(КлючИЗначение.Ключ,"_",".") Тогда
			
			НовоеПодменю = Элементы.Добавить("ГР_Подменю1", Тип("ГруппаФормы"), Элементы.ГР_КоманднаяПанель);
			НовоеПодменю.Вид = ВидГруппыФормы.ГруппаКнопок;
			НовоеПодменю.Заголовок = "Действия";
			
			Для каждого ЭлементКода из КлючИЗначение.Значение Цикл
				
				НоваяКоманда = Команды.Добавить(ЭлементКода.Имя);
				НоваяКоманда.Действие = "ГР_ДобавитьКод_ОбработчикКоманды";
				
				НоваяКнопка = Элементы.Добавить(ЭлементКода.Имя, Тип("КнопкаФормы"), НовоеПодменю);
				НоваяКнопка.Заголовок = ЭлементКода.Наименование;
				НоваяКнопка.ИмяКоманды = ЭлементКода.Имя;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ГР_ОКВместо(Команда)
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ИзменятьВТранзакции",    ИзменятьВТранзакции);
	РезультатВыбора.Вставить("ОбрабатыватьРекурсивно", ОбрабатыватьРекурсивно);
	РезультатВыбора.Вставить("НастройкаПорции",        НастройкаПорции);
	РезультатВыбора.Вставить("ПроцентОбъектовВПорции", ПроцентОбъектовВПорции);
	РезультатВыбора.Вставить("ЧислоОбъектовВПорции",   ЧислоОбъектовВПорции);
	РезультатВыбора.Вставить("ПрерыватьПриОшибке",     ИзменятьВТранзакции Или ПрерыватьПриОшибке);
	РезультатВыбора.Вставить("ПоказыватьСлужебныеРеквизиты", ПоказыватьСлужебныеРеквизиты);
	РезультатВыбора.Вставить("РежимРазработчика", РежимРазработчика);
	РезультатВыбора.Вставить("ОтключитьСвязиПараметровВыбора", ОтключитьСвязиПараметровВыбора);
	
	РезультатВыбора.Вставить("ГР_ВыполнитьКодПередЗаписьюЭлемента", ЭтаФорма.ГР_ВыполнитьКодПередЗаписьюЭлемента);
	РезультатВыбора.Вставить("ГР_КодПередЗаписью", ЭтаФорма.ГР_КодПередЗаписью);
	
	ОповеститьОВыборе(РезультатВыбора);
КонецПроцедуры


&НаКлиенте
Процедура ГР_ПриИзменении_ГР_ВыполнитьКодПередЗаписьюЭлемента(Элемент)
	Элементы.ГР_КодПередЗаписью.Видимость = Этаформа.ГР_ВыполнитьКодПередЗаписьюЭлемента;
	Элементы.ГР_КоманднаяПанель.Видимость = ЭтаФорма.ГР_ВыполнитьКодПередЗаписьюЭлемента;
КонецПроцедуры

&НаКлиенте
Процедура ГР_ДобавитьКод_ОбработчикКоманды(Элемент)
	ДобавитьКод(Элемент.Имя);
КонецПроцедуры

&НаСервере
Процедура ДобавитьКод(ИмяЭлементаКода);
	Коды = ГР_ПолучитьТекстыОбработчиков();
	Для Каждого КлючИЗначение из Коды Цикл
		Если ЭтаФорма.ГР_ВидыИзменяемыхОбъектов = СтрЗаменить(КлючИЗначение.Ключ,"_",".") Тогда
			Для каждого ЭлементКода из КлючИЗначение.Значение Цикл
				Если ЭлементКода.Имя = ИмяЭлементаКода Тогда
					ЭтаФорма.ГР_КодПередЗаписью = ЭтаФорма.ГР_КодПередЗаписью+
					?(пустаястрока(ЭтаФорма.ГР_КодПередЗаписью),"",Символы.ПС)+
					"//"+ЭлементКода.Наименование+Символы.ПС+стрзаменить(ЭлементКода.Код,"\", Символы.ПС); 
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
Конецпроцедуры

&НаСервереБезКонтекста
Функция ГР_ПолучитьТекстыОбработчиков()
	
	Макет = Обработки.ГрупповоеИзменениеРеквизитов.ПолучитьМакет("ГР_ШаблоныКода");
	Текст = Макет.ПолучитьТекст();

	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Текст);
	Данные = ПрочитатьJSON(ЧтениеJSON, Ложь);
	Возврат Данные;

КонецФункции